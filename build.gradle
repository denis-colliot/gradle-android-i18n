buildscript {
    ext.versions = [
            java         : '1.8',
            junit        : '4.12',
            jackson      : '2.9.7',
            poi          : '4.0.0',
            jcifs        : '1.3.17',
            mockitoKotlin: "1.6.0",
            assertJ      : "3.11.1",
    ]
}

plugins {
    id 'java-gradle-plugin' // Adds 'gradleApi()' and 'gradleTestKit()' dependencies.
    id 'org.jetbrains.kotlin.jvm' version '1.3.11'
    id 'com.gradle.plugin-publish' version '0.10.0'
}

group = 'com.github.gradle'
version = '1.3'

repositories {
    jcenter()
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.jetbrains.kotlin') {
            details.useVersion '1.3.11'
            details.because 'All Kotlin APIs should use the same version'
        }
    }
}

dependencies {
    testImplementation "junit:junit:${versions.junit}"
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:${versions.mockitoKotlin}"
    testImplementation "org.assertj:assertj-core:${versions.assertJ}"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:${versions.jackson}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:${versions.jackson}"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:${versions.jackson}"
    implementation "org.apache.poi:poi-excelant:${versions.poi}"
    implementation "jcifs:jcifs:${versions.jcifs}"
}

compileKotlin {
    kotlinOptions.jvmTarget = versions.java
}
compileTestKotlin {
    kotlinOptions.jvmTarget = versions.java
}

test {
    reports {
        junitXml.enabled = true
        html.enabled = true
    }
}

// -----------------------------------------------------------------------------------------
//
// Publish configuration.
//
// -----------------------------------------------------------------------------------------

pluginBundle {
    website = 'https://github.com/denis-colliot/gradle-android-i18n'
    vcsUrl = 'https://github.com/denis-colliot/gradle-android-i18n'
    description = 'Gradle plugin for android generating localized resources from an external translations source.'
    tags = ['android', 'i18n', 'localized', 'translations']

    plugins {
        androidI18nPlugin {
            id = 'com.github.gradle.android-i18n'
            displayName = 'Gradle android i18n plugin'
        }
    }
}